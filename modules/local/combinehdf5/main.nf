/*
 * Merge all per-metric HDF5 files produced for a single run into one combined HDF5 file
 **/
process COMBINEHDF5 {
    tag "${meta.id}"
    label 'process_single'

    container "ghcr.io/mpc-bioinformatics/macproqc-helpers:sha-914105c"

    input:
    tuple val(meta), path(hdf5_files)

    output:
    tuple val(meta), path("*.hdf5"), emit: hdf5
    tuple val("${task.process}"), val('macproqc-helpers'), val("sha-914105c"), topic: versions, emit: versions_combinehdf5

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    python -m macproqc_helpers combine-hdf5 \\
        ${args} \\
        -hdf_out_name ${prefix}.hdf5 \\
        ${hdf5_files}
    """

    stub:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    echo ${args}

    touch ${prefix}.hdf5
    """
}
